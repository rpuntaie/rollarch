# Maintainer:  Michael Daffin <michael@daffin.io>

_pkgname=vim-prettier
_pkgauthor=prettier
pkgname=neovim-prettier
pkgver=0.2.7
pkgrel=1
pkgdesc="A Vim plugin for Prettier"
arch=('any')
url="https://github.com/${_pkgauthor}/${_pkgname}"
license=('MIT')
groups=('neovim-plugins')
depends=('neovim' 'prettier')
source=("https://github.com/${_pkgauthor}/${_pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('53466d1763a8da5232c66028905b814a6a064b52305aeec0ea738275815c2776')

package() {
  cd "vim-prettier-${pkgver}"
  local installpath="$pkgdir/usr/share/nvim/runtime/pack/${_pkgauthor}/start/${_pkgname}"
  mkdir -p "$installpath/"{autoload,doc,ftdetect,ftplugin,plugin}
  install -Dm644 autoload/*.vim "$installpath/autoload/"
  install -Dm644 doc/*.txt "$installpath/doc/"
  install -Dm644 ftdetect/*.vim "$installpath/ftdetect/"
  install -Dm644 ftplugin/*.vim "$installpath/ftplugin/"
  install -Dm644 plugin/*.vim "$installpath/plugin/"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
