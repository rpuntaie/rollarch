# Based on Michael Daffin <michael@daffin.io>
# modified to cope with missing packages and to fit to different install script
# meta packages were combined, but installed packages are (almost) as before
#
# # curl -OLs git://git.io/installarch
# # DSK=/dev/sda USR=u PW=p HST=up ZONE=London LA_NG=en_GB IP2=1.121 AIP2=1.108 ./installarch mdaffin-meta
#
# change u, p, IP2 and AIP2

pkgname=(mdaffin-meta)
pkgver=30
pkgrel=1
pkgdesc="System config for mdaffin systems"
arch=(any)
license=(MIT)

source=(
    mdaffin-base.sh
    mdaffin-base.vim
    mdaffin-base.sudoers
    mdaffin-base-zsh.local
    mdaffin-base-vconsole.conf
    mdaffin-base-enp.network
    mdaffin-base-wlp.network
    mdaffin-gui-sddm.conf
    mdaffin-gui-00-keyboard.conf
    mdaffin-gui-50-mouse-acceleration.conf
    mdaffin-gui-i3-config
    mdaffin-gui.termite
    mdaffin-gui.sh
    mdaffin-devel-rust.vim
    mdaffin-devel-rust.sh
    mdaffin-devel-go.vim
    mdaffin-devel-go.sh
    mdaffin-devel-web.sh
    mdaffin-devel-web.vim
)

# The sources all live in this repo and so are version controlled and checked
# summed through git. There is not much point in adding these extra checksums
# here espically when they have to be changed for every update to them.
sha256sums=(SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP SKIP)

package_mdaffin-meta() {
    provides=(vim vi)
    conflicts=(vim vi)
    install=mdaffin-gui.install

    # Base group
    # Explicitly removed from the base group: jfsutils lvm2 nano reiserfsprogs vi xfsprogs
    depends=(bash bzip2 coreutils cryptsetup device-mapper dhcpcd diffutils
    e2fsprogs file filesystem findutils gawk gcc-libs gettext glibc grep gzip
    inetutils iproute2 iputils less licenses linux linux-firmware logrotate man-db man-pages
    mdadm netctl pacman pciutils perl procps-ng psmisc s-nail sed shadow
    sysfsutils systemd-sysvcompat tar texinfo usbutils util-linux which)

    # partly needed by rollarchroot
    depends+=(nfs-utils samba dialog wpa_supplicant ntp openssh devtools sudo git python)

    # Extra general packages
    depends+=(stow ripgrep exa fd tmux wget fzf unzip dialog pacman-contrib bat ncdu keybase)

    install -Dm 0644 mdaffin-base-vconsole.conf "${pkgdir}/etc/vconsole.conf"
    install -Dm 0640 mdaffin-base.sudoers "${pkgdir}/etc/sudoers.d/mdaffin-base"

    # Debugging tools
    depends+=(lsof bind-tools mtr socat htop iotop openbsd-netcat strace)

    # Networking
    depends+=(wpa_supplicant openssh)

    # Shells
    depends+=(zsh grml-zsh-config)

    install -Dm 0755 mdaffin-base.sh "${pkgdir}/etc/profile.d/mdaffin-base.sh"
    install -Dm 0644 mdaffin-base-zsh.local "${pkgdir}/etc/zsh/zshrc.local"

    # Filesystems
    depends+=(exfat-utils dosfstools f2fs-tools)

    install -Dm 0644 mdaffin-base-enp.network "${pkgdir}/usr/lib/systemd/network/90-mdaffin-base-enp.network"
    install -Dm 0644 mdaffin-base-wlp.network "${pkgdir}/usr/lib/systemd/network/90-mdaffin-base-wlp.network"

    # Base Devel Group
    depends+=(autoconf automake binutils bison fakeroot file findutils flex gawk gcc gettext
    grep groff gzip libtool m4 make pacman patch pkgconf sed systemd texinfo
    util-linux which)

    # General tools
    depends+=(cmake jq terraform)

    # Editor
    depends+=(neovim python-neovim neovim-ale)
    install -dm 0755 "${pkgdir}/usr/bin"
    ln -s /usr/bin/nvim "${pkgdir}/usr/bin/vim"
    ln -s /usr/bin/nvim "${pkgdir}/usr/bin/vi"
    install -dm755 "${pkgdir}/usr/share/nvim/runtime/plugin/"
    ln -sf /usr/share/vim/vimfiles/plugin/fzf.vim "${pkgdir}/usr/share/nvim/runtime/plugin/fzf.vim"
    install -Dm 0644 mdaffin-base.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-base.vim"

    # Virtualisation
    depends+=(virtualbox-guest-modules-arch virtualbox-guest-utils virtualbox-host-modules-arch virtualbox vagrant)

    # Fonts
    depends+=(noto-fonts noto-fonts-cjk noto-fonts-emoji noto-fonts-extra
    ttf-dejavu xorg-fonts-misc)

    # Docker
    depends+=(docker docker-compose dnsmasq)

    # Rust
    depends+=(neovim-rust-git rustup)
    install -Dm 0644 mdaffin-devel-rust.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-devel-rust.vim"
    install -Dm 0755 mdaffin-devel-rust.sh "${pkgdir}/etc/profile.d/mdaffin-devel-rust.sh"

    # Go
    depends+=(go neovim-go)
    install -Dm 0644 mdaffin-devel-go.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-devel-go.vim"
    install -Dm 0755 mdaffin-devel-go.sh "${pkgdir}/etc/profile.d/mdaffin-devel-go.sh"

    # Node
    depends+=(yarn npm nodejs prettier)
    install -Dm 0644 mdaffin-devel-web.vim "${pkgdir}/usr/share/nvim/runtime/plugin/mdaffin-devel-web.vim"
    install -Dm 0755 mdaffin-devel-web.sh "${pkgdir}/etc/profile.d/mdaffin-devel-web.sh"

    depends+=(qemu qemu-arch-extra picocom ddrescue arm-none-eabi-gcc)

    # I3 window manager
    depends+=(rofi i3-wm i3status i3blocks i3lock dmenu compton)
    install -Dm 0644 mdaffin-gui-i3-config "${pkgdir}/etc/i3/mdaffin-gui-config"

    # Login manager
    depends+=(sddm rng-tools)
    install -Dm 0644 mdaffin-gui-sddm.conf "${pkgdir}/etc/sddm.conf"

    # Applications
    depends+=(alacritty firefox)
    install -Dm 0644 mdaffin-gui.termite "${pkgdir}/etc/xdg/termite/mdaffin-gui.config"

    # Utility
    depends+=(redshift python-gobject bluez bluez-utils pulseaudio pulseaudio-alsa pulseaudio-bluetooth pavucontrol paprefs scrot)

    # Drivers and utilities
    depends+=(xf86-input-libinput xf86-input-synaptics
    xf86-video-intel xf86-video-vesa nvidia nvidia-utils)

    # Xorg packages and utilities
    depends+=(xorg-xsetroot xorg-xkill xorg-xbacklight xorg-bdftopcf
    xorg-iceauth xorg-luit xorg-mkfontdir xorg-mkfontscale xorg-sessreg
    xorg-setxkbmap xorg-smproxy xorg-x11perf xorg-xauth xorg-xbacklight
    xorg-xcmsdb xorg-xcursorgen xorg-xdpyinfo xorg-xdriinfo xorg-xev
    xorg-xgamma xorg-xhost xorg-xinput xorg-xkbcomp xorg-xkbevd xorg-xkbutils
    xorg-xkill xorg-xlsatoms xorg-xlsclients xorg-xmodmap xorg-xpr xorg-xprop
    xorg-xrandr xorg-xrdb xorg-xrefresh xorg-xset xorg-xsetroot xorg-xvinfo
    xorg-xwd xorg-xwininfo xorg-xwud)

    install -Dm 0644 mdaffin-gui-00-keyboard.conf "${pkgdir}/etc/X11/xorg.conf.d/00-keyboard.conf"
    install -Dm 0644 mdaffin-gui-50-mouse-acceleration.conf "${pkgdir}/etc/X11/xorg.conf.d/50-mouse-acceleration.conf"
    install -Dm 0755 mdaffin-gui.sh "${pkgdir}/etc/X11/xinit/xinitrc.d/30-mdaffin-gui.sh"
}

